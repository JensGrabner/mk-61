; Преобразование даты и времени в MJD
; ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
; (C) 1982 Сергей Вакуленко, Алексей Калиш
;
; MJD - модифицированная юлианская дата - применяется
; в любительской астрономии. Обозначает количество суток,
; прошедших с 00:00 17 ноября 1858 года.
; http://ru.wikipedia.org/wiki/Юлианская_дата
;
; После ввода программы нужно:
;  - в регистр 4 ввести число -678957;
;  - в регистр 6 ввести число 60;
;  - в регистр 7 ввести число 365;
;  - в регистр 8 ввести число 153.
;
; Процесс вычисления:
; 1) Ввести год, В^.
; 2) Ввести месяц, В/О, С/П.
; 3) Ввести число, В^.
; 4) Ввести часы, В^.
; 5) Ввести минуты, В^.
; 6) Ввести секунды, В/О, С/П.
; После окончания работы программы на индикаторе высвечивается
; целая часть MJD. Дробную часть можно найти в регистре 3,
; целую часть - в регистре 2, год - в регистре 1, месяц - в регистре 0,
; число - в регистре A, часы - в регистре B, минуты - в регистре C,
; секунды - в регистре D,
;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;; 00
        хП 9            ; Месяц или секунды
        F ⟳             ; Год или минуты
        Пх 6            ; 60
        -
        F x<0
            год_и_число
        Пх 6            ; 60
        +
        хП c            ; Минуты
        F ⟳
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;; 10
        хП b            ; Часы
        F ⟳
        хП a            ; Число
        Пх 9
        хП d            ; Секунды
        Пх 5            ; MJD на начало месяца
        Пх a
        +               ; Прибавим число
        хП 2            ; Целая часть MJD
        Пх d            ; Секунды
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;; 20
        Пх 6            ; 60
        /               ; Секунды/60
        Пх c            ; Минуты
        +               ; Минуты + секунды/60
        Пх 6            ; 60
        /               ; Минуты/60 + секунды/3600
        Пх b            ; Часы
        +
        2
        4
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;; 30
        /
        хП 3            ; Часы/24 + минуты/60/24 + секунды/3600/24
        Пх 2            ; Дробная часть MJD
        С/П             ; Останов

год_и_число:
        Пх 6            ; 60
        +
        хП 1            ; Год
        Пх 9
        хП 0            ; Месяц
        Пх 1
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;; 40
        3
        +
        4
        /
        хП 9            ; (год + 3) / 4
        K Пх 9          ; Целая часть
        Пх 9
        Пх 7            ; 365
        Пх 1            ; Год
        x               ; Год * 365
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;; 50
        +               ; Год*365 + [(год+3)/4]
        Пх 4            ; -678957
        +
        хП 5            ; Год*365 + [(год+3)/4] - 678957
        Пх 0            ; Месяц
        3
        -               ; Месяц - 3
        F x<0
            март_и_дальше
        2
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;; 60
        +
        3
        1
        x               ; (месяц-1) * 31
к_результату:
        Пх 5
        +
        хП 5            ; Результат MJD
        С/П             ; Останов

март_и_дальше:
        4
        +               ; Месяц + 1
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;; 70
        Пх 8            ; 153
        x
        5
        /
        хП 9            ; ((месяц+1) * 153) / 5
        K Пх 9          ; Целая часть
        Пх 9
        6
        3
        -               ; [((месяц+1) * 153) / 5] - 63
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;; 80
        Пх 5
        +
        хП 5            ; Год*365 + [(год+3)/4] - 678957 + [((месяц+1) * 153) / 5] - 63
        Пх 1            ; Год
        4
        /
        хП 9            ; Год / 4
        K Пх 9          ; Целая часть
        <->
        Пх 9
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;; 90
        -               ; Дробная часть
        F x=0
            невисокосный
        1               ; Високосный год
        БП              ; Прибавим один день
            к_результату

невисокосный:
        Пх 5            ; Результат
        С/П             ; Останов
